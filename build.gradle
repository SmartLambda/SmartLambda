import org.apache.tools.ant.taskdefs.condition.Os

plugins {
    id 'de.fuerstenau.buildconfig' version '1.1.7'
}

group 'edu.teco'
version '1.0-SNAPSHOT'

apply plugin: 'application'
apply plugin: 'idea'

mainClassName = "edu.teco.smartlambda.Application"
sourceCompatibility = 1.8
docsDirName = "../docs"

idea {
    module {
        sourceDirs += file("build/gen/buildconfig/src/main")
    }
}

repositories {
    mavenCentral()
}

dependencies {
    // runtime dependencies
    compile group: 'com.sparkjava', name: 'spark-core', version: '2.5.4'
    compile group: 'com.google.guava', name: 'guava', version: '19.0'
    compile group: 'com.google.code.gson', name: 'gson', version: '2.8.0'
    compile group: 'org.apache.commons', name: 'commons-configuration2', version: '2.1'
    compile group: 'commons-beanutils', name: 'commons-beanutils', version: '1.9.3'
    compile group: 'org.quartz-scheduler', name: 'quartz', version: '2.2.1'
    compile group: 'org.hibernate', name: 'hibernate-core', version: '5.2.7.Final'
    compile group: 'org.hibernate', name: 'hibernate-c3p0', version: '5.2.7.Final'
    compile group: 'mysql', name: 'mysql-connector-java', version: '5.1.6'
    compile group: 'com.fasterxml.jackson.core', name: 'jackson-core', version: '2.8.6'
    compile group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.8.6'
    compile group: 'org.torpedoquery', name: 'org.torpedoquery', version: '2.2.1'
    compile group: 'org.slf4j', name: 'slf4j-simple', version: '1.7.23'
    compile group: 'com.spotify', name: 'docker-client', version: '7.0.2'
    compile group: 'de.mkammerer', name: 'argon2-jvm', version: '2.2'
    compile group: 'commons-io', name: 'commons-io', version: '2.5'
    compile project(":shared")

    // compile-time dependencies
    compileOnly group: 'org.projectlombok', name: 'lombok', version: '1.16.12'
    testCompileOnly group: 'org.projectlombok', name: 'lombok', version: '1.16.12'

    // test dependencies
    testCompile group: 'junit', name: 'junit', version: '4.11'
    testCompile group: 'org.mockito', name: 'mockito-core', version: '2.7.4'
    testCompile group: 'org.powermock', name: 'powermock-module-junit4', version: '1.7.0RC4'
    testCompile group: 'org.powermock', name: 'powermock-api-mockito2', version: '1.7.0RC4'
}

buildConfig {
    appName = project.name
    version = project.version
    packageName = project.group + "." + "smartlambda"
    if (Os.isFamily(Os.FAMILY_WINDOWS)) {
        buildConfigField "String", "CONFIGURATION_PATH", "C:\\\\smartlambda\\\\smartlambda.xml"
        buildConfigField "String", "HIBERNATE_CONFIGURATION_PATH", "C:\\\\smartlambda\\\\hibernate.xml"
    } else {
        buildConfigField "String", "CONFIGURATION_PATH", "/etc/smartlambda/smartlambda.xml"
        buildConfigField "String", "HIBERNATE_CONFIGURATION_PATH", "/etc/smartlambda/hibernate.xml"
    }
}

javadoc {
    options {
        showFromProtected()
        noTimestamp true
        links "http://docs.oracle.com/javase/8/docs/api"
    }
}

processResources.dependsOn ':execution-jre8:install'